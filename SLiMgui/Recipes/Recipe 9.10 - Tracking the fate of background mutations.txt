initialize() {
	defineConstant("L", 3e6);
	initializeMutationRate(1e-6);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeMutationType("m2", 0.8, "f", 0.5);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-7);
}
1 {
	sim.addSubpop("p1", 500);
}
1000 late() {
	target = sample(p1.genomes, 1);
	defineConstant("BACKGROUND", target.mutations);
	mut = target.addNewDrawnMutation(m2, asInteger(L/2));
	defineConstant("SWEEP", mut);
}
1000: late() {
	if (!SWEEP.isSegregating & !SWEEP.isFixed)
		stop("LOST");
}
1500 late() {
	nonSeg = BACKGROUND[!BACKGROUND.isSegregating];
	fixed = nonSeg[nonSeg.isFixed];
	lost = nonSeg[!nonSeg.isFixed];
	writeFile("~/Desktop/fixed.txt", paste(fixed.position, sep=", "));
	writeFile("~/Desktop/lost.txt", paste(lost.position, sep=", "));
}
