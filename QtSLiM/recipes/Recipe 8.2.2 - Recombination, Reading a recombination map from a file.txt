// Keywords: recombination rate map, recombination map

initialize() {
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 23011543);
	
	// read Drosophila 2L map from Comeron et al. 2012
	// this file is in the recipe archive at http://benhaller.com/slim/SLiM_Recipes.zip
	map = readCSV("Comeron_100kb_chr2L.txt", c("ends", "rates"), sep="\t");
	ends = map.getValue("ends");
	ends = c(ends[1:(size(ends)-1)] - 2, 23011543);
	rates = map.getValue("rates") * 1e-8;
	initializeRecombinationRate(rates, ends);
}
1 early() { sim.addSubpop("p1", 500); }
10000 early() { sim.simulationFinished(); }
