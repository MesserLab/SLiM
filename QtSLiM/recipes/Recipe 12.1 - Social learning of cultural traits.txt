// Keywords: non-genetic inheritance

initialize() {
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);   // neutral
	initializeMutationType("m2", 0.5, "f", 0.1);   // lactase-promoting
	m2.convertToSubstitution = F;
	initializeGenomicElementType("g1", c(m1,m2), c(0.99,0.01));
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-8);
}
1 early() {
	sim.addSubpop("p1", 1000);
	p1.individuals.tagL0 = (runif(1000) < 0.5);
}
modifyChild() {
	parentCulture = mean(c(parent1.tagL0, parent2.tagL0));
	childCulture = (runif(1) < 0.1 + 0.8 * parentCulture);
	child.tagL0 = childCulture;
	return T;
}
mutationEffect(m2) {
	if (individual.tagL0)
		return effect;      // beneficial for milk-drinkers
	else
		return 1.0;         // neutral for non-milk-drinkers
}
10000 early() { sim.simulationFinished(); }
