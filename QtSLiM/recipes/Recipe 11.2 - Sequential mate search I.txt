// Keywords: choosy, choosiness, ornament, mate choice

initialize() {
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);     // neutral
	initializeMutationType("m2", 0.5, "f", -0.025);  // ornamental
	initializeGenomicElementType("g1", c(m1, m2), c(1.0, 0.01));
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-8);
}
1 { sim.addSubpop("p1", 500); }
1:10001 early() {
	if (sim.generation % 1000 == 1) {
		fixedMuts = sum(sim.substitutions.mutationType == m2);
		osize = fixedMuts * 2 + p1.individuals.countOfMutationsOfType(m2);
		catn(sim.generation + ": Mean ornament size == " + mean(osize));
	}
}
mateChoice() {
	fixedMuts = sum(sim.substitutions.mutationType == m2);
	for (attempt in 1:5)
	{
		mate = sample(p1.individuals, 1, T, weights);
		osize = fixedMuts * 2 + mate.countOfMutationsOfType(m2);
		
		if (runif(1) < log(osize + 1) * 0.1 + attempt * 0.1)
			return mate;
	}
	return float(0);
}
